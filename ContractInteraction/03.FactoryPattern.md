Factory 패턴은 스마트 컨트랙트 개발에서 여러 개의 동일한 종류의 컨트랙트를 효율적으로 생성하고 관리하기 위해 사용하는 디자인 패턴이다.   
Solidity에서는 Factory 패턴을 통해 특정 컨트랙트를 생성하는 컨트랙트를 만들 수 있다. gi이렇게 하면 코드가 더 모듈화되고, 생성된 컨트랙트를 쉽게 추적하고 관리할 수 있다는 장점이 있다!

---
### 예제 코드 전체
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;


contract Car {
    address public owner;
    string public model;
    address public carAddr;

    constructor(address _owner, string memory _model) payable {
        owner = _owner;
        model = _model;
        carAddr = address(this);
    }
}

contract CarFactory {
    Car[] public cars;

    function create(address _owner, string memory _model) public {
        Car car = new Car(_owner, _model);
        cars.push(car);
    }

    function createAndSendEther(address _owner, string memory _model)
        public
        payable
    {
        Car car = (new Car){value: msg.value}(_owner, _model);
        cars.push(car);
    }

    function getCar(uint256 _index)
        public
        view
        returns (
            address owner,
            string memory model,
            address carAddr,
            uint256 balance
        )
    {
        Car car = cars[_index];

        return (car.owner(), car.model(), car.carAddr(), address(car).balance);
    }
}
```

 Car 컨트랙트를 생성하고 관리하는 CarFactory 컨트랙트를 구현한 예제이다. 

 ### create 함수
 ```solidity
 function create(address _owner, string memory _model) public {
    Car car = new Car(_owner, _model);
    cars.push(car);
}
```
새로운 `Car`객체(컨트랙트)를 생성하고, cars라는 배열에 push

### createAndSendEther 함수
```solidity
function createAndSendEther(address _owner, string memory _model)
    public
    payable
{
    Car car = (new Car){value: msg.value}(_owner, _model);
    cars.push(car);
}
```
새로운 `Car` 객체를 생성하면서 이더를 함께 전송할 수도 있다. 새로 만들어지는 `Car` 컨트랙트 주소에 이더가 들어가게 된다. 

### getCar 함수

```solidity
function getCar(uint256 _index)
    public
    view
    returns (
        address owner,
        string memory model,
        address carAddr,
        uint256 balance
    )
{
    Car car = cars[_index];

    return (car.owner(), car.model(), car.carAddr(), address(car).balance);
}
```
`_index`를 통해 생성된 `Car` 컨트랙트의 owner, model, carAddr, balance를 조회

