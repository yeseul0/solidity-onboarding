# External Call 


Solidity에서 외부 컨트랙트를 호출하는 것은 다른 스마트 계약과 상호작용할 수 있는 중요한 기능이다. 외부 컨트랙트 호출 방법에는 두 가지가 있다.

1. **직접 호출** : `A.foo(x,y)`와 같이 함수명을 직접 호출
2. **저수준 호출** : `call`을 이용하여 호출. (에러 핸들링이 어렵고 보안 문제가 있을 수 있어 권장되지 않는다.)

---
## 직접호출
직접호출은 외부 컨트랙트의 특정 함수를 호출할 때, 해당 함수의 **인터페이스(ABI)** 를 통해 호출하는 방식이다.  
Solidity에서는 **인터페이스** 또는 **컨트랙트 타입**을 사용하여 명시적으로 호출한다. 
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

contract CallerContract {
    function setX(address _address, uint256 _x) external {
        CalledContract(_address).setX(_x);
    }
}

contract CalledContract {
    uint public x;
    uint public value = 123;

    function setX(uint _x) external {
        x = _x;
    }
}
```

- `CalledContract`: 상태 변수 x를 관리하며, 외부에서 값을 설정할 수 있는 컨트랙트.
- `CallerContract`: 다른 컨트랙트(`CalledContract`)의 함수를 호출하는 역할을 수행.
- `CallerContract`가 `CalledContract`의 주소를 알고 있어야 하며, 이를 통해 `setX`를 호출하여 `x` 값을 변경
> 실습 순서  
> `CalledContract` `CallerContract`배포  
> `CallerContract`의 `setX` 함수를 호출하면서, 배포된 `CalledContract`의 주소와 값을 전달

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

contract CallerContractAndSendEther {
    function setXAndReceiveEther(address _address, uint256 _x) external payable {
        CalledContractReceivesEther(_address).receiveXandreceiveEther{value: msg.value}(_x);
    }
}

contract CalledContractReceivesEther {
    uint public x;
    uint public value = 123;

    function receiveXandreceiveEther(uint _x) external payable {
        x = _x;
        value = msg.value;
    }
}
```
`CalledContractReceivesEther(_address).receiveXandreceiveEther{value: msg.value}(_x)`를 통해 `_address`에 있는 `CalledContractReceivesEther`를 호출하고, `_x` 값과 이더를 `msg.value`만큼 전달

> 실습 순서
> `CalledContractReceivesEther` `CallerContractAndSendEther` 배포
> `CallerContractAndSendEther`의 `setXAndReceiveEther` 함수를 호출하면서, 배포된 `CalledContractReceivesEther`의 주소, `_x` 값, 그리고 이더를 전달

---
## 저수준 호출(Low Level Call)
저수준 호출은 함수 이름이나 ABI 없이, EVM의 `call`과 같은 기능으로 외부 컨트랙트를 호출하는 방식이다. 
ABI 없이 호출해야하기 때문에 함수 시그니처, 매개변수 등을 직접 인코딩하여 호출해야 하며, 더 유연하지만 그만큼 위험성이 높다.  
-> 주로 직접 호출이 불가능한 경우나 컨트랙트 주소가 동적으로 결정되는 경우 사용된다.
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.3;

contract CallerContractAndSendEther {
    // CalledContractReceivesEther의 receiveXandreceiveEther를 호출하고 반환값을 디코딩합니다.
    function setXAndReceiveEther(address _address, uint256 _x) external payable returns (uint256, uint256) {
        // 함수 호출에 필요한 데이터를 인코딩합니다.
        (bool success, bytes memory data) = _address.call{value: msg.value}(
            abi.encodeWithSignature("receiveXandreceiveEther(uint256)", _x)
        );

        // 호출 성공 여부를 확인합니다.
        require(success, "Low-level call failed");

        // 반환값을 디코딩합니다.
        (uint256 returnedX, uint256 returnedValue) = abi.decode(data, (uint256, uint256));
        return (returnedX, returnedValue);
    }
}

contract CalledContractReceivesEther {
    uint public x;
    uint public value;

    // 외부에서 호출 가능한 함수로, x와 msg.value를 업데이트하고 반환합니다.
    function receiveXandreceiveEther(uint _x) external payable returns (uint256, uint256) {
        x = _x;
        value = msg.value;

        // x와 value 값을 반환합니다.
        return (x, value);
    }
}
```

- `abi.encodeWithSignature("receiveXandreceiveEther(uint256)", _x)`  
호출하려는 함수의 이름과 매개변수 타입을 포함한 시그니처, `_x` 값 인코딩하여 호출데이터로 변환.   
-> `receiveXandreceiveEther(uint256)` 함수와 `_x` 값이 포함된 데이터가 생성되는 꼴이다. 
- `_address.call{value: msg.value}(...)`  
    - `_address`에 있는 컨트랙트를 호출
    - `{value: msg.value}`를 통해 이더를 전송
    - `(...)` 호출 데이터는 ABI 인코딩된 값으로 전달
- `call`의 반환값 두가지
    - `success`: 호출 성공 여부를 나타내는 bool 값.
    - `data`: 호출된 함수의 반환 데이터를 포함하는 bytes 값.
- `abi.decode(data, (uint256, uint256))`  
    - CalledContract에서 반환된 데이터를 디코딩한다.
    - (uint256 returnedX, uint256 returnedValue)에 저장된다.  
